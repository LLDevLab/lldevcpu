// Get calibration of bmp085 device
bmp085_get_calibrations:
push r0
push r1
push r2
push r3
push r4
push r5
push r6
// r0 - calibration registers base address
// r1 - loop iteration counter
// r2 - loop max iteration
// r3 - holds loop increment value
// r4 - register, that holds temporary values
// r5 - register to hold received calibration value
// r6 - index of calibration array
ldi r0, #0xaa
clr r1
// 22 because 11 msb values and 11 lsb values
ldi r2, #22
ldi r3, #1
clr r6
// Send calibration registers msb
bmp085_get_calibrations_loop:
// send start condition
ldi r4, i2c_send_start_signal
call r4
// send bmp085 write address
ldi r4, bmp085_send_write_addr
call r4
// send bmp085 register address to read
mov r4, r0
add r4, r1
push r4
ldi r4, bmp085_send_data
call r4
pop r4
// read data from sensor
// send start condition
ldi r4, i2c_send_start_signal
call r4
// send bmp085 read address
ldi r4, bmp085_send_read_addr
call r4
// Send msb calibration register's address
mov r4, r0
add r4, r1
push r4
ldi r4, bmp085_send_data
call r4
// save current value of r0
push r0
ldi r4, i2c_receive_data
call r4
mov r5, r0
lsh r5, #8
// restore value of r0
pop r0
pop r4
// Send lsb calibration register's address
add r1, r3
mov r4, r0
add r4, r1
push r4
ldi r4, bmp085_send_data
call r4
push r0
ldi r4, i2c_receive_data
call r4
or r5, r0
pop r0
pop r4
add r1, r3
ldi r4, i2c_send_stop_signal
call r4
// pass second parameter to function
push r6
// pass first parameter to function
push r5
ldi r5, heap_save
call r5
pop r5
pop r6
add r6, r3
ldi r4, bmp085_get_calibrations_loop
cmp r1, r2
brne r4
// Restore the stack and return from function
pop r6
pop r5
pop r4
pop r3
pop r2
pop r1
pop r0
ret

// Sending a write address to bmp085
bmp085_send_write_addr:
push r0
ldi r0, #0xee
// pass parameter to the function
push r0
ldi r0, bmp085_send_data
call r0
pop r0
// Restore the stack and return from the function
pop r0
ret

// Sending a read address to bmp085
bmp085_send_read_addr:
push r0
ldi r0, #0xef
// pass parameter to the function
push r0
ldi r0, bmp085_send_data
call r0
pop r0
// Restore the stack and return from the function
pop r0
ret

// Sending data to bmp085 device
// Arguments:
// 	- A data to send
bmp085_send_data:
push r12
mov r12, r14
push r0
push r1
// Get first parameter from the stack
// Set map of RAM addresses
ldi r0, #2
lsh r0, #28
// Or'ing stack frame with the map
or r0, r12
// Parameter will have an offset r12 + 3
ldi r1, #3
add r0, r1
// Load parameter's value to r1 register
ld r1, r0
// Set parameter's data to "i2c data input/output register"
push r1
ldi r1, i2c_set_data
call r1
pop r1
// Start data transmission
ldi r1, i2c_send_data
call r1
// Restore the stack and return from function
pop r1
pop r0
pop r12
ret
